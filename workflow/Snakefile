# Snakemake workflow

import os

configfile: "config/config.yaml"

include: "rules/simulations.smk"
include: "rules/analyses.smk"

rule all:
	input:
		expand("results/simulations/scenario_{sc}.{ext}", sc=config['scenarios'], ext=['yaml', 'json', 'svg']),
		expand('results/figures/fig_sim_msprime_scenario_{sc}.pdf', sc=config['scenarios']),
		expand(
			'results/figures/fig_sim_slim_sel_scenario_{sc}_{shift_type}_t{time}_s{ssize}.pdf',
			sc=config['scenarios'],
			shift_type=['gradual'],
			time=[140],
			ssize=[3.0],
		),
		"results/figures/fig_sim_main.pdf",
		'results/figures/fig_sim_msprime_europe_uk.pdf',

rule prepare_scenarios:
	output:
		"results/simulations/scenario_{sc}.yaml",
		"results/simulations/scenario_{sc}.json",
		"results/simulations/scenario_{sc}.svg",
	params:
		outdir = lambda w, output: os.path.dirname(output[0]),
	conda:
		"envs/popgensim.yaml"
	script:
		"scripts/prep_scenario.py"

rule main_figure:
	input:
		sim_neutral = 'results/simulations/sim_msprime_scenario_2A.pickle',
		sim_sel = 'results/simulations/sim_slim_sel_scenario_2A_gradual_t140_s3.0.pickle',
	output:
		fig = "results/figures/fig_sim_main.pdf",
	params:
		sel_title = "gradual selection",
	conda:
		"envs/popgensim.yaml"
	script:
		"scripts/main_figure.py"